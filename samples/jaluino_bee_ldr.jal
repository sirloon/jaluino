-- Title: Light sensor LDR sample for Jaluino Bee
-- Author: Sebastien Lelong, Copyright (c) 2008..2009, all rights reserved.
-- Adapted-by:
-- Compiler: 2.4o
--
-- This file is part of jaluino  (http://jaluino.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: this program show how to read data from a LDR (photoresistor).
-- This basically means reading value from ADC. Using Seeedstudio's Grove brick
-- (http://www.seeedstudio.com/depot/grove-light-sensor-p-746.html?cPath=144_148)
-- just power the sensor with 5V or 3V3, connect RA5 pin (which is analog pin #4)
-- to SIG pin on the sensor. Results are printed through serial.
--


include jaluino_bee
include delay

onboard_led_direction =  output

-- we'll print results through serial
const SERIAL_HW_BAUDRATE = 9600
include serial_hardware
serial_hw_init()

include print

const byte welcome[] = "-- LDR sample --"
const byte str_light[] = "Light value: "


-- Configure ADC
const byte ADC_NVREF = ADC_NO_EXT_VREF
const word ADC_RSOURCE = 2_000
const bit ADC_HIGH_RESOLUTION = high
include adc
adc_init()
set_analog_pin(4) -- RA5/AN*4*

var word light_value = 0 -- will store result from ADC readings

print_string(serial_hw_data,welcome)
print_crlf(serial_hw_data)
print_crlf(serial_hw_data)

forever loop
   onboard_led = on
   
   light_value =   adc_read(4) -- read on ADC channel #4
   print_string(serial_hw_data,str_light)
   print_word_dec(serial_hw_data,light_value)
   print_crlf(serial_hw_data)
   
   onboard_led = off
   
   delay_1s(1)
end loop

--
