-- Title: Minix FS/OS demo for Jaluino Bee, based on 18F27J53 
-- Author: SÃ©bastien Lelong, Copyright (c) 2011, all rights reserved.
-- Adapted-by:
-- Compiler: 2.4o
--
-- This file is part of jaluino  (http://jaluino.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: Minix demo. Explore Minix filesystem and perform actions
-- using some sort of *nix-like commands:
--    * ls   : list directories or file's details
--    * cd   : change directory
--    * fsck : show super-block details
--    * cat  : print file content
--    * dcat : "dev" cat, showing address details and content when changing
--    * $?   : print last errors
--
--

-- we plan to use SD-Card so tell the main library to 
-- configure remappable pins and create appropriate aliases
const byte ENABLE_MSSP2_SDCARD = 1

include jaluino_bee
include print
include delay
include jascii
include strings

onboard_led_direction = output
onboard_led = off

const serial_hw_baudrate = 115_200
include serial_hardware
serial_hw_init()

alias minix_goto_address is sd_start_read
alias minix_close_block is sd_stop_read
alias minix_data_byte is sd_data_byte -- get|put byte
alias minix_print_carrier is serial_hw_data
include minixfs

const byte WELCOME[] = "Welcome to Minix OS :)"
const byte PROMPT[] = "minix> "
print_crlf(serial_hw_data)
print_string(serial_hw_data,WELCOME)
print_crlf(serial_hw_data)

-- some sort of "mount"
minix_init(true)

function atoi(byte in nums[]) return byte is
   var byte num = 0
   var byte numdec = 0
   numdec = nums[0] -- blocking
   -- no number at all ?
   if numdec == ASCII_NULL then
      return 0
   end if
   num = nums[1]
   -- no decimals ?
   if num == ASCII_NULL then
      num = numdec
      numdec = "0"
   end if
   return byte(((numdec - "0") * 10) + (num - "0"))
end function

procedure reboot() is
   asm goto 0x00
end procedure

-- Minix commands for REPL impl.
const byte MINIX_FSCK_CMD[] = "fsck\0"
const byte MINIX_DOLLARQ_CMD[] = "$?\0"
const byte MINIX_CAT_CMD[] = "cat\0"
const byte MINIX_DCAT_CMD[] = "dcat\0"
const byte MINIX_CD_CMD[] = "cd\0"
const byte MINIX_LS_CMD[] = "ls\0"
const byte MINIX_REBOOT_CMD[] = "reboot\0"
   
var byte command[16]
var byte filename[MINIX_MAX_FILENANE_LEN]
var byte char
var byte i
forever loop

   -- print prompt :)
   print_string(serial_hw_data,PROMPT)
   
   -- init to no command and no filename
   command[0] = 0
   filename[0] = 0

   -- read command
   i = 0
   while true  loop
      char = serial_hw_data
      -- echo
      serial_hw_data = char
      if char == ASCII_LF | char == ASCII_CR | char == ASCII_SP then
         command[i] = ASCII_NULL -- NULL terminated string
         exit loop
      end if
      command[i] = char
      i = i + 1
   end loop

   -- read filename if necessary
   if char == ASCII_SP then
      i = 0
      while true  loop
         char = serial_hw_data
         -- echo
         serial_hw_data = char
         if char == ASCII_LF | char == ASCII_CR | char == ASCII_SP then
            filename[i] = ASCII_NULL -- NULL terminated string
            exit loop
         end if
         filename[i] = char
         i = i + 1
      end loop
   end if

   if char == ASCII_LF | char == ASCII_CR | char == ASCII_SP then
      print_crlf(serial_hw_data)
   end if

   if command[0] != ASCII_NULL then
      if strcmp(command,MINIX_FSCK_CMD) then
         minix_dev_print_super_block()
      elsif strcmp(command,MINIX_DOLLARQ_CMD) then
         minix_dev_print_error()
      elsif strcmp(command,MINIX_CD_CMD) then
         minix_cd(filename)
      elsif strcmp(command,MINIX_CAT_CMD) then
         minix_cat(filename)
      elsif strcmp(command,MINIX_DCAT_CMD) then
         minix_dev_cat(filename)
      elsif strcmp(command,MINIX_LS_CMD) then
         minix_ls(filename)
      elsif strcmp(command,MINIX_REBOOT_CMD) then
         reboot()
      end if
   end if

end loop

