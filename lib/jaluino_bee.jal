-- ------------------------------------------------------
-- Title: Jaluino Bee device file (wrapper over 18f27j53.jal)
-- Author: Sebastien Lelong, Copyright (c) 2008..2009, all rights reserved.
-- Adapted-by:
-- Compiler: 2.4n
-- Revision: $Revision$
--
-- This file is part of jaluino  (http://jaluino.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: this is the main file included in every jaluino project
-- that uses Jaluino Bee hardware. Jaluino Bee is based on PIC 18F27J53.
-- This files performs basic setup to configure PIC, like setting clock speed
-- to 48MHz, disable watchdog, etc...
-- --
-- The idea is you just have to type "include jaluino_bee" and your chip
-- is configured and ready to be used !
-- .
-- Prior including this lib, more configuration can be performed, particularly
-- setting the following constant to 1 or other specific values:
--    * JALUINO_SKIP_PINMAP : won't create pin aliases, like "onboard_led"
--    * ENABLE_MSSP2_SPI    : will configure PPS module for MSSP2 usage,
--    * ENABLE_MSSP2_SDCARD : will auto-set ENABLE_MSSP2_SPI = 1 and configure
--                            MSSP2 to user SD-Card
--    * ENABLE_PPS_RX2TX2_B7B6 : prepare pins through PPS for serial hardware 2
--                               module usage (RX2 <=> RB7, TX2 <=> RB6)
--    * ENABLE_SERIAL_HW: use passed value as baud speed and configure
--                        serial hardware
--    * ENABLE_SERIAL_HW2: same as before, for second serial hardware module
--
-- Sources:
--
-- Notes:
--

include 18f27j53
pragma target clock 48_000_000   -- oscillator frequency
-- magical statements
pragma target PLLDIV	   P4
pragma target CPUDIV	   P1
pragma target PLLEN  P1          -- PLL via fuse turned off
pragma target OSC	   HS_PLL
pragma target WDT  disabled      -- no watchdog
pragma target IOL1WAY disabled   -- Mapping allowed multiple time at runtime
OSCTUNE_PLLEN = on               -- PLL via OSCTUNE enabled
-- declare secondary oscillator on T1OSI/T1OSO pins
pragma target RTCOSC T1OSC
pragma target SOSCSEL HS_CP
_usec_delay(2000) -- PLL needs 2ms before stable

enable_digital_io()

-- Include official pin mapping. Since it's arbitrary, one may want to bypass it
-- and provides his own mapping, defining the following constant.
if defined(JALUINO_SKIP_PINMAP) == false then
   include jaluino_bee_pinmap
end if

include pps

-- Configure Remappable Pins on MSSP2 to match SPI/SD-Card wiring,
-- configure MSSP2 and initialize SD-card library
if defined(ENABLE_MSSP2_SDCARD) then
   if !defined(ENABLE_MSSP2_SPI) then
      const byte ENABLE_MSSP2_SPI = 1
   end if
end if

if defined(ENABLE_MSSP2_SPI) then
   -- PPS module writes are protected, we need to first unlock it
   pps_control_lock(false)
   -- RP3 <-> /SS2
   PPS_MAP_SS2IN = RP3
   -- RP2 <-> SCK2
   PPS_MAP_RP2 = PPS_SCK2
   PPS_MAP_SCK2IN = RP2
   -- RP5 <-> SDI2
   PPS_MAP_SDI2 = RP5
   -- RP6 <-> SDO2
   PPS_MAP_RP6 = PPS_SDO2
   -- PPS configuration is done, we can lock again
   pps_control_lock(true)

   alias pin_ss2_direction is pin_b0_direction
   alias pin_ss2 is pin_b0
   alias pin_sdi2_direction is pin_b2_direction
   alias pin_sdo2_direction is pin_b3_direction
   alias pin_sck2_direction is pin_a5_direction

   -- configure SPI to use MSSP2 module
   include spi_master_hw2
   pin_sdi2_direction = input    -- spi input
   pin_sdo2_direction = output   -- spi output
   pin_sck2_direction = output   -- spi clock
   spi2_init(SPI_MODE_11,SPI_RATE_FOSC_4) -- init spi, choose mode and speed

end if

if defined(ENABLE_MSSP2_SDCARD) then

   -- setup the sd card SPI "Slave Select" pin
   alias sd_chip_select is pin_ss2
   alias sd_chip_select_direction is pin_ss2_direction
   sd_chip_select_direction = output
   sd_chip_select = high

   -- Aliases used for generic access for SD-card, pointing to MSSP2 related procedures
   alias spi_master_set_mode is spi_master_hw2_set_mode
   alias spi_master_set_speed is spi_master_hw2_set_speed

   -- setup the sd card library
   ;const bit SD_EXTRA_SPEED = TRUE
   const bit SD_ALWAYS_SET_SPI_MODE = TRUE
   const bit SD_DELAY_AFTER_WRITE = TRUE
   -- define which data carrier to use to access sd-card
   alias spi_master is spi_master_hw2
   
   if defined(ENABLE_SDCARD_DEBUG) then
      const byte SD_CARD_USE_DEBUG_PROC = true
   end if
   include sd_card
   sd_init()
   
end if

if defined(ENABLE_MSSP2_23K256) then
   if !defined(ENABLE_MSSP2_SDCARD) then
      _error "You need to ENABLE_MSSP2_SDCARD to setup MSSP2"
   end if
   alias sram_23k256_chip_select is pin_b1
   alias sram_23k256_chip_select_direction is pin_b1_direction
   -- initial settings
   sram_23k256_chip_select_direction = output -- chip select/slave select pin
   sram_23k256_chip_select = high             -- start chip slect high (chip disabled)
   -- initalize 23k256 in byte mode, always set spi mode to 0,0
   alias sram_23k256_force_spi_mode is spi_master_hw2_set_mode_00
   -- setup Microchip 23k256 sram
   include sram_23k256
   -- init 23k256 in sequential mode
   sram_23k256_init(SRAM_23K256_SEQUENTIAL_MODE, SRAM_23K256_HOLD_DISABLE)

end if

-- configure pins through PPS as:
--  * B7 <= RX2
--  * B6 => TX2
if defined(ENABLE_PPS_RX2TX2_B7B6) then

   include pps
   -- PPS module writes are protected, we need to first unlock it
   pps_control_lock(false)
   -- TX2 is an output function: we assign a function to a RP pin (here RP9)
   PPS_MAP_RP9 = PPS_TX2CK2
   -- RX2 is an input function: we assign a RP pin the a function register
   PPS_MAP_RX2DT2 = RP10
   -- PPS configuration is done, we can lock again to apply changes
   pps_control_lock(true)

   -- declare aliases for the remapped RX2 and TX2 pins
   alias pin_RX2            is  pin_RP10
   alias pin_RX2_direction  is  pin_RP10_direction
   alias pin_TX2            is  pin_RP9
   alias pin_TX2_direction  is  pin_RP9_direction

   -- PIC is ready to use serial_hardware2
   
end if

if defined(ENABLE_SERIAL_HW2) then
   const serial_hw2_baudrate = ENABLE_SERIAL_HW2
   include serial_hardware2
   serial_hw2_init()
end if

if defined(ENABLE_SERIAL_HW) then
   const serial_hw_baudrate = ENABLE_SERIAL_HW
   include serial_hardware
   serial_hw_init()
end if

-- that's all, PIC is ready !

