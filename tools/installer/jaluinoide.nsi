;------------------------------------------------------------------------------
; JaluinoIDE Windows Installer Build Script, loosely based on Editra NSIS script
; Author: Cody Precord / Sébastien Lelong
; Language: NSIS
; Licence: wxWindows License
;------------------------------------------------------------------------------


;------------------------------ Start MUI Setup -------------------------------

; Global Variables
!define PRODUCT_NAME "Jaluino"
!define EDITRA_NAME "Editra"
!define PRODUCT_PUBLISHER "Jaluino Group"
!define PRODUCT_WEB_SITE "http://jaluino.org"
!define EDITRA_WEB_SITE "http://editra.org"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\${PRODUCT_NAME}"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define ENVIRON_KEY "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"

; build-time generated by "make release"
!include "jaluino.nsh"

SetCompressor lzma

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "..\..\doc\materials\jaluinoide.ico"
!define MUI_UNICON "..\..\doc\materials\jaluinoide.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME

; License page (Read the Licence), Editra + dual Jaluino license
!insertmacro MUI_PAGE_LICENSE "..\..\3rdparty\Editra\dist\COPYING"
!insertmacro MUI_PAGE_LICENSE "..\distrib\jaluino-${PRODUCT_VERSION}\LICENSE.bsd"
!insertmacro MUI_PAGE_LICENSE "..\distrib\jaluino-${PRODUCT_VERSION}\LICENSE.cc"

; Components Page (Select what parts to install)
!insertmacro MUI_PAGE_COMPONENTS

; Directory page (Set Where to Install)
!insertmacro MUI_PAGE_DIRECTORY

; Instfiles page (Do the installation)
!insertmacro MUI_PAGE_INSTFILES

; Finish page (Post installation tasks)
!define MUI_FINISHPAGE_RUN
!define MUI_FINISHPAGE_RUN_TEXT "Run JaluinoIDE, powered by Editra"
!define MUI_FINISHPAGE_RUN_FUNCTION "LaunchEditra"
!insertmacro MUI_PAGE_FINISH

; Un-Installer pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS

;------------------------------- End MUI Setup --------------------------------

 
;------------------------------ Start Installer -------------------------------

;---- Constants
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "jaluinoide-${PRODUCT_VERSION}.exe"
InstallDir "$PROGRAMFILES\${PRODUCT_NAME}"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

;---- !defines for use with SHChangeNotify
!ifdef SHCNE_ASSOCCHANGED
!undef SHCNE_ASSOCCHANGED
!endif
!define SHCNE_ASSOCCHANGED 0x08000000

!ifdef SHCNF_FLUSH
!undef SHCNF_FLUSH
!endif
!define SHCNF_FLUSH        0x1000

; Prepare for installation
Function .onInit

	SetOutPath $TEMP
	File /oname=spltmp.bmp "..\..\doc\materials\jaluinoide_splash.bmp"

	splash::show 2000 $TEMP\spltmp

	Pop $0 	; $0 has '1' if the user closed the splash screen early,
			; '0' if everything closed normally, and '-1' if some error occurred.

	Delete $TEMP\spltmp.bmp

  ; prevent running multiple instances of the installer
  System::Call 'kernel32::CreateMutexA(i 0, i 0, t "jaluinoide_installer") i .r1 ?e'
  Pop $R0
  StrCmp $R0 0 +3
    MessageBox MB_OK|MB_ICONEXCLAMATION "The installer is already running."
    Abort

  ; Check for existing installation warn before installing new one
  ReadRegStr $R0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
  StrCmp $R0 "" done

  MessageBox MB_YESNO|MB_ICONEXCLAMATION \
  "An existing installation of ${PRODUCT_NAME} has been found. $\nDo you want to remove the previous version before installing $(^Name) ?" \
  IDNO done

  ; Run the uninstaller
  ClearErrors
  ExecWait '$R0 _?=$INSTDIR' ; Do not copy the uninstaller to a temp file

  done:
FunctionEnd

 
; Extract the files from the installer to the install location
Section "Jaluino" SEC01
  SectionIn RO 1 2
 
  ; Check that Editra is not running before starting to copy the files
  FindProcDLL::FindProc "${EDITRA_NAME}.exe"
  StrCmp $R0 0 continueInstall
    MessageBox MB_ICONSTOP|MB_OK "${EDITRA_NAME} is still running please close all running instances and try to install again"
    Abort
  continueInstall:

  ; Extract the files and make shortcuts
  SetOverwrite try 
    
  SetOutPath "$INSTDIR\"
  File /r "..\distrib\jaluino-${PRODUCT_VERSION}\*"
  
  SetOutPath "$INSTDIR\3rdparty\${EDITRA_NAME}"
  File /r "..\..\3rdparty\Editra\dist\*.*"

  ; Add the shortcuts to the start menu and desktop
  CreateDirectory "$SMPROGRAMS\${PRODUCT_NAME}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Editra.lnk" "$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe" "" "$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Documentation.lnk" "$INSTDIR\doc"
  CreateShortCut "$DESKTOP\Editra.lnk" "$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe"  "" "$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe"
  
SectionEnd

; Enabled if Add openwith entry is checked
Section "Context Menus" SEC02
  SectionIn 1
  WriteRegStr HKCR "*\shell\OpenWithEditra" "" "Edit with ${EDITRA_NAME}"
  WriteRegStr HKCR "*\shell\OpenWithEditra\command" "" '$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe "%1"'
;  WriteRegStr HKCR "*\shell\OpenWithEditra\DefaultIcon" "" "$INSTDIR\Editra.exe,1"

  ; Notify of the shell extension changes
  System::Call 'Shell32::SHChangeNotify(i ${SHCNE_ASSOCCHANGED}, i ${SHCNF_FLUSH}, i 0, i 0)'
SectionEnd

; Add QuickLaunch Icon (That small icon bar next to the start button)
Section "Add Quick Launch Icon" SEC03
  SectionIn 1
  CreateShortCut "$QUICKLAUNCH\Editra.lnk" "$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe"
SectionEnd

; Make/Install Shortcut links
Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\3rdparty\${EDITRA_NAME}\${EDITRA_NAME}.url" "InternetShortcut" "URL" "${EDITRA_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Editra Website.lnk" "$INSTDIR\3rdparty\${EDITRA_NAME}\${EDITRA_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Jaluino Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
  CreateShortCut "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

; Post installation setup
Section -Post
  ;---- Write registry keys for uninstaller
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\doc\materials\jaluinoide.ico"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  
  ; if Editra hasn't run, xml and plugins files can't be installed. Prepare directories
  CreateDirectory "$APPDATA\Editra\plugins"
  CreateDirectory "$APPDATA\Editra\cache"
  CopyFiles "$INSTDIR\ide\plugins\*.egg" "$APPDATA\Editra\plugins"
  CopyFiles "$INSTDIR\ide\conf\*" "$APPDATA\Editra\cache"
  
  ; install and configure JaluinoIDE
  ; prepare environment...
  System::Call 'Kernel32::SetEnvironmentVariableA(t, t) i("PYTHONPATH", "$INSTDIR\3rdparty\Editra\library.zip").r0'
  StrCmp $0 0 error
	ExecWait '"$INSTDIR\3rdparty\Editra\python.exe" "$INSTDIR\install.py"'
  Goto done
  error:
    MessageBox MB_OK|MB_ICONEXCLAMATION "Can't set PYTHONPATH environment variable"
  done:
  
  ; XWisp programmer GUI uses Tcl libraries, we need to setup TCL_LIBRARY envar
  ; so they can be found (Ooooh Lord this all thing really is a mess...)
  ; FIXME: if this variable exists, it'll be overridden and even deleted when uninstalled.
  ; I could use http://nsis.sourceforge.net/Environmental_Variables:_append,_prepend,_and_remove_entries
  ; but yet another thing to install before having a working building environment...
  
  WriteRegStr HKLM "${ENVIRON_KEY}" "TCL_LIBRARY" "$INSTDIR\3rdparty\Editra\tcl\tcl8.5"
  ; "Export" our change
  SendMessage ${HWND_BROADCAST} ${WM_WININICHANGE} 0 "STR:Environment" /TIMEOUT=5000

SectionEnd

; Called if Run Editra is checked on the last page of installer
Function LaunchEditra
  Exec '"$INSTDIR\3rdparty\${EDITRA_NAME}\Editra.exe" "$INSTDIR\samples\jaluino_medium_blink.jal" '
FunctionEnd

; Description Texts for Component page
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "JaluinoIDE with Editra, jallib libraries, samples, documentation, jalv2 compiler, bootloaders, ..."
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "Add context menu item 'Edit with Editra'"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC03} "Add shortcut to Quick Launch Bar"
!insertmacro MUI_FUNCTION_DESCRIPTION_END
;------------------------------- End Installer --------------------------------


;----------------------------- Start Uninstaller ------------------------------

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ; Remove all Files
  RmDir /r "$INSTDIR\"

  ; Remove all shortcuts
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Uninstall.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Jaluino Website.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Editra Website.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Editra.lnk"
  Delete "$SMPROGRAMS\${PRODUCT_NAME}\Documentation.lnk"
  Delete "$DESKTOP\Editra.lnk"
  Delete "$QUICKLAUNCH\Editra.lnk"
  Delete "$INSTDIR\uninst.exe"
  RMDir "$SMPROGRAMS\${PRODUCT_NAME}"
  
  

  ; Cleanup Registry
  DeleteRegKey HKCR "*\shell\OpenWithEditra"
  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegValue HKLM "${ENVIRON_KEY}" "TCL_LIBRARY"

  SetAutoClose true
SectionEnd

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

;------------------------------ End Uninstaller -------------------------------
