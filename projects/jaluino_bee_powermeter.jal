-- Title: Powermeter with Jaluino Bee.
-- Author: Sebastien Lelong, Copyright (c) 2011, all rights reserved.
-- Adapted-by:
-- Compiler: 2.4o
--
-- This file is part of jaluino (http://jaluino.googlecode.com)
-- Released under the BSD license (http://www.opensource.org/licenses/bsd-license.php)
--
-- Description: Some electrical counter device has a flashing LED giving information
-- about current consumption. This program uses a phototransistor connected on RA1,
-- and detect these flashes, using interrupts. Each flash increments a counter.
-- Using Realtime clock & calendar peripheral, it triggers some alarm events
-- on regular basis, and send flash count through serial. This allows to collect
-- flash count each minute, for instance.
--

-- chip setup
include jaluino_bee

include delay
include print

onboard_led_direction = output
onboard_led =  off

-- map INT1 on RA1 (which is remappable pin RP1)
pps_control_lock(false)
PPS_MAP_INT1 = RP1
pps_control_lock(true)


const SERIAL_HW_BAUDRATE = 9600
include serial_hardware
serial_hw_init()

const byte STR_DATA[] = "Data: "
const byte STR_WELCOME[] = "Power Meter"

print_string(serial_hw_data,STR_WELCOME)
print_crlf(serial_hw_data)


include rtcc_hw

-- RTC can be calibrated, 32KHz Xtal must be measured
-- in order to determine the deviation. This is specific to 
-- the crystal itself. We're assuming no deviation.
rtc_calibrate(0)

-- let's say we're April 23rd 2011, it's 10:56:12 o'clock.
-- Alarm is set at 10:56:17
rtc_set_date(0x11,0x04,0x23)           -- YY/MM/DD )
rtc_set_time(0x10,0x56,0x12)           -- HH:MM:SS ) BCD format
rtc_set_alarm_time(0x10,0x56,0x17)     -- HH:MM:SS )
rtc_set_alarm_interval(RTC_ALARM_INTERVAL_TENSECONDS)
rtc_set_alarm_repeat(255)
rtc_set_alarm(ON)                      -- enable alarm
rtc_set_clock(ON)                      -- enable RTCC module

-- set up interrupts
INTCON3_INT1IE = on -- allow interrupt from changing state on INT1
INTCON3_INT1IF = off -- flush flag of interrupt from INT1
INTCON2_INTEDG1 = 0 -- setting up interrupt INT1 from changing state 1->0
                    -- this corresponds to no-light -> light
-- Set interrupts so we can react when alarm is ringing
PIE3_RTCCIE = enabled
-- allow interrupts
INTCON_GIE = on
INTCON_PEIE = on

var byte year
var byte month
var byte day
var byte hour
var byte minute
var byte second

var word flash_count
var bit do_send_data = false

procedure int_isr() is
   pragma interrupt
   
   -- INT1 interrupt: there was a flash
   if INTCON3_INT1IF then
      onboard_led = !onboard_led
      flash_count = flash_count + 1
      INTCON3_INT1IF = off -- flushing flag
   end if
   
   -- RTC interrupts: need to send flash count value
   if PIR3_RTCCIF then
      do_send_data = true
      PIR3_RTCCIF = false -- clear flag
   end if

end procedure

-- main loop
forever loop

   if do_send_data then
   
      do_send_data = false
      print_crlf(serial_hw_data)
      rtc_get_date(year,month,day)
      rtc_get_time(hour,minute,second)
      print_string(serial_hw_data,STR_DATA)
      print_byte_hex(serial_hw_data,year)
      serial_hw_data = "-"
      print_byte_hex(serial_hw_data,month)
      serial_hw_data = "-"
      print_byte_hex(serial_hw_data,day)
      serial_hw_data = " "
      print_byte_hex(serial_hw_data,hour)
      serial_hw_data = ":"
      print_byte_hex(serial_hw_data,minute)
      serial_hw_data = ":"
      print_byte_hex(serial_hw_data,second)
      serial_hw_data = "|"
      print_word_dec(serial_hw_data,flash_count)
      print_crlf(serial_hw_data)
      -- reset flash counter
      flash_count = 0
      
   end if
   
   delay_1s(1)
   serial_hw_data = "."
   
end loop

